--Onur Aktas, Jacob Hallman and Adrian LÃ¶kk
MODULE main
VAR
  choice:  boolean;
  payment: boolean;
  acc_payment: boolean; 
  n_items: 0..5;
  state:   { ready, expect_payment, dispense_item };

ASSIGN
  init (state) := ready;
  next (state) := case
		    state = ready          & choice:  expect_payment;
		    state = expect_payment & acc_payment: dispense_item;
		    state = expect_payment & !choice & !payment: ready;
		    state = dispense_item:	      ready;
		    TRUE:                             state;
  esac;

  init (acc_payment) := FALSE;
  next (acc_payment) := (state = expect_payment & payment & n_items > 0);

  next(n_items) := case
		     state = dispense_item & n_items > 0: n_items - 1;
                     TRUE: 				  n_items;
  esac;

DEFINE
  release_item := (state = dispense_item);

LTLSPEC G(acc_payment -> F release_item);
-- Number of items should always be â‰¥ 0.
LTLSPEC G(n_items >= 0);
-- Payment should only be accepted if number of items > 0.
LTLSPEC G(acc_payment -> n_items > 0);
-- If an item is dispensed, the counter of items is always reduced by 1.
LTLSPEC G(state = dispense_item -> next(n_items) = n_items - 1);

-- specification  G (acc_payment ->  F release_item)  is false
-- as demonstrated by the following execution sequence
--Trace Description: LTL Counterexample 
--Trace Type: Counterexample 
--  -> State: 1.1 <-
--    choice = FALSE
--    payment = FALSE
--    acc_payment = FALSE
--    state = ready
--    release_item = FALSE
--  -> State: 1.2 <-
--    choice = TRUE * we make a choice
--  -> State: 1.3 <-
--    choice = FALSE
--    payment = TRUE * we pay, but regret our choice
--    state = expect_payment
--  -> State: 1.4 <-
--    payment = FALSE
--    acc_payment = TRUE
--    state = ready * the machine resets, despite accepting payment
--  -- Loop starts here
--  -> State: 1.5 <-
--    acc_payment = FALSE
--  -> State: 1.6 <-

